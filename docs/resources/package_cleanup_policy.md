---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "artifactory_package_cleanup_policy Resource - terraform-provider-artifactory"
subcategory: "Configuration"
description: |-
  Provides an Artifactory Package Cleanup Policy resource. This resource enable system administrators to define and customize policies based on specific criteria for removing unused binaries from across their JFrog platform. See Cleanup Policies https://jfrog.com/help/r/jfrog-platform-administration-documentation/cleanup-policies for more details.
---

# artifactory_package_cleanup_policy (Resource)

Provides an Artifactory Package Cleanup Policy resource. This resource enable system administrators to define and customize policies based on specific criteria for removing unused binaries from across their JFrog platform. Package cleanup policies are supported on the Cloud (7.98.2) and Self-Hosted (7.98.7) platforms, with an Enterprise+ license. See [Cleanup Policies](https://jfrog.com/help/r/jfrog-platform-administration-documentation/cleanup-policies) for more details.

## Example Usage

### Time-based Cleanup Policy (Days)

```terraform
resource "artifactory_package_cleanup_policy" "my-cleanup-policy" {
  key = "my-cleanup-policy"
  description = "My cleanup policy"
  cron_expression = "0 0 2 ? * MON-SAT *"
  duration_in_minutes = 60
  enabled = true
  skip_trashcan = false
  
  search_criteria = {
    package_types = ["docker", "gradle", "maven",]
    repos = ["**"]
    include_all_projects = true
    included_projects = []
    included_packages = ["**"]
    excluded_packages = ["com/jfrog/latest"]
    created_before_in_days = 30
    last_downloaded_before_in_days = 60
  }
}
```

### Version-based Cleanup Policy

```terraform
resource "artifactory_package_cleanup_policy" "my-version-policy" {
  key = "my-version-policy"
  description = "Keep only latest versions"
  cron_expression = "0 0 2 ? * MON-SAT *"
  duration_in_minutes = 60
  enabled = true
  skip_trashcan = false
  
  search_criteria = {
    package_types = ["maven"]
    repos = ["**"]
    include_all_projects = true
    included_projects = []
    included_packages = ["**"]
    excluded_packages = ["com/jfrog/latest"]
    keep_last_n_versions = 5
  }
}
```

### Properties-based Cleanup Policy

```terraform
resource "artifactory_package_cleanup_policy" "my-properties-policy" {
  key = "my-properties-policy"
  description = "Cleanup based on properties"
  cron_expression = "0 0 2 ? * MON-SAT *"
  duration_in_minutes = 60
  enabled = true
  skip_trashcan = false
  
  search_criteria = {
    package_types = ["docker"]
    repos = ["**"]
    include_all_projects = true
    included_projects = []
    included_packages = ["**"]
    excluded_packages = ["com/jfrog/latest"]
    included_properties = {
      "build.name" = ["my-app"]
    }
  }
}
```

### Project-level Cleanup Policy

```terraform
resource "artifactory_local_docker_v2_repository" "my-docker-local" {
  key             = "my-docker-local"
  tag_retention   = 3
  max_unique_tags = 5

  lifecycle {
    ignore_changes = ["project_key", "project_environments"]
  }
}

resource "project" "myproj" {
  key = "myproj"
  display_name = "myproj"
  description  = "Test Project"
  admin_privileges {
    manage_members   = true
    manage_resources = true
    index_resources  = true
  }
  max_storage_in_gibibytes   = 10
  block_deployments_on_limit = false
  email_notification         = true
}

resource "project_repository" "myproj-my-docker-local" {
  project_key = project.myproj.key
  key = artifactory_local_docker_v2_repository.my-docker-local.key
}

resource "artifactory_package_cleanup_policy" "my-project-cleanup-policy" {
  key = "myproj-cleanup-policy"
  project_key = project.myproj.key
  description = "My project-level cleanup policy"
  cron_expression = "0 0 2 ? * MON-SAT *"
  duration_in_minutes = 60
  enabled = true
  skip_trashcan = false
  
  search_criteria = {
    package_types = ["docker"]
    repos = [project_repository.myproj-my-docker-local.key]
    include_all_projects = false
    included_projects = []
    included_packages = ["**"]
    excluded_packages = ["com/jfrog/latest"]
    created_before_in_days = 30
  }
}
```

### Using Variables for Condition Fields

You can use Terraform variables for condition fields (`created_before_in_days`, `last_downloaded_before_in_days`, `created_before_in_months`, `last_downloaded_before_in_months`, `keep_last_n_versions`, `included_properties`) and `duration_in_minutes`. The validator will skip validation when values are unknown (variables), allowing `terraform validate` to pass without requiring variable values.

**Example with variables:**

```terraform
variable "cleanup_policy_last_downloaded_before_in_days" {
  type    = number
  default = 60
}

variable "cleanup_policy_duration_in_minutes" {
  type    = number
  default = 120
}

resource "artifactory_package_cleanup_policy" "my-cleanup-policy" {
  key = "my-cleanup-policy"
  description = "My cleanup policy with variables"
  cron_expression = "0 0 2 ? * MON-SAT *"
  duration_in_minutes = var.cleanup_policy_duration_in_minutes
  enabled = true
  skip_trashcan = false
  
  search_criteria = {
    package_types = ["docker", "generic", "helm", "helmoci", "nuget", "terraform"]
    repos = ["**"]
    include_all_projects = false
    included_projects = ["default"]
    included_packages = ["**"]
    excluded_packages = ["com/jfrog/latest"]
    last_downloaded_before_in_days = var.cleanup_policy_last_downloaded_before_in_days
  }
}
```

**Important Notes:**
- Variables with default values allow `terraform validate` to pass without requiring variable values
- Variables without default values will require values to be provided during `terraform plan` or `terraform apply`
- The validator automatically skips validation when condition field values are unknown (variables), preventing false validation errors during `terraform validate`

## Validation Rules

The cleanup policy resource enforces the following validation rules:

1. **Condition Types**: A policy must use exactly one of the following condition types:
   - Time-based conditions (`days-based`)
   - Version-based condition (`keep_last_n_versions`)
   - Properties-based condition (`included_properties`)

2. **Mutual Exclusivity**: Cannot use multiple condition types together.

3. **Zero Values**: Time-based and version-based conditions must have values greater than 0.

4. **Days vs Months**: Cannot use both days-based conditions (`created_before_in_days`, `last_downloaded_before_in_days`) and months-based conditions (`created_before_in_months`, `last_downloaded_before_in_months`) together.

5. **Properties Validation**: Properties-based conditions must have exactly one key with exactly one string value.

6. **Project Configuration**: When `include_all_projects` is set to `true`, the `included_projects` field can be empty array. When `include_all_projects` is `false`, `included_projects` must contain at least one project key.

7. **Project-level Policy Constraints**: When `project_key` is specified (project-level policy):
   - `include_all_projects` must be set to `false`
   - `included_projects` should be empty array `[]`
   - Policy `key` must start with the project key value as a prefix followed by a hyphen
     - ✅ Valid: `project_key = "myproj"` → `key = "myproj-cleanup-policy"`
     - ❌ Invalid: `project_key = "myproj"` → `key = "cleanup-policy"` (missing prefix)
     - ❌ Invalid: `project_key = "myproj"` → `key = "other-cleanup-policy"` (wrong prefix)

## Supported Package Types

The following package types are supported for cleanup policies with their respective minimum Artifactory versions:

- **alpine** - Alpine Linux packages (supported from 7.108.0)
- **ansible** - Ansible collections and roles (supported from 7.104.2)
- **cargo** - Rust Cargo packages (supported from 7.102.0)
- **chef** - Chef cookbooks (supported from 7.112.0)
- **cocoapods** - CocoaPods packages (supported from 7.99.1)
- **composer** - PHP Composer packages (supported from 7.116.0)
- **conan** - Conan C/C++ packages (supported from 7.98.2)
- **conda** - Conda packages (supported from 7.105.2)
- **debian** - Debian packages (supported from 7.98.2)
- **docker** - Docker images (supported from 7.98.2, version-based condition (keep_last_n_versions) from 7.115.1)
- **gems** - Ruby gems (supported from 7.96.3)
- **generic** - Generic packages (supported from 7.98.2, version-based conditions is not supported)
- **go** - Go modules (supported from 7.98.2)
- **gradle** - Gradle packages (supported from 7.98.2)
- **helm** - Helm charts (supported from 7.98.2)
- **helmoci** - Helm OCI charts (supported from 7.102.0, version-based conditions (keep_last_n_versions) from 7.115.1)
- **huggingfaceml** - Hugging Face ML models (supported from 7.100.0)
- **machinelearning** - Machine learning models (supported from 7.104.2)
- **maven** - Maven packages (supported from 7.98.2)
- **npm** - Node.js packages (supported from 7.98.2)
- **nuget** - .NET NuGet packages (supported from 7.98.2)
- **oci** - OCI images (supported from 7.90.1, version-based conditions (keep_last_n_versions) from 7.115.1)
- **puppet** - Puppet modules (supported from 7.112.0)
- **pypi** - Python packages (supported from 7.98.2)
- **rpm|yum** - RPM packages (supported from 7.98.2)
- **sbt** - Scala SBT packages (supported from 7.108.0)
- **swift** - Swift packages
- **terraform** - Terraform modules (supported from 7.99.1)
- **terraformbackend** - Terraform backend configurations (supported from 7.103.0)

## Version Compatibility

- The `created_before_in_days` and `last_downloaded_before_in_days` attributes are only supported in Artifactory 7.111.2 and later. For earlier versions, use `created_before_in_months` and `last_downloaded_before_in_months`.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `key` (String) An ID that is used to identify the cleanup policy. A minimum of three characters is required and can include letters, numbers, underscore and hyphen.
- `search_criteria` (Attributes) (see [below for nested schema](#nestedatt--search_criteria))

### Optional

- `cron_expression` (String) The cron expression that determines when the policy is run, However if left empty the policy will not run automatically and can only be triggered manually.
- `description` (String)
- `duration_in_minutes` (Number) The maximum duration (in minutes) for policy execution, after which the policy will stop running even if not completed. While setting a maximum run duration for a policy is useful for adhering to a strict cleanup schedule, it can cause the policy to stop before completion.
- `enabled` (Boolean) A cleanup policy must be created inactive. But if used it must be set to `false`. If set to `true` when calling this API, the API call will fail and an error message is received. Defaults to `true`
- `project_key` (String) This attribute is used only for project-level cleanup policies, it is not used for global-level policies. When specified, the policy will be scoped to the specified project. Note: The policy `key` must start with this project key value as a prefix (e.g., if `project_key` is `"myproj"`, the `key` should be `"myproj-policy-name"`).
- `skip_trashcan` (Boolean) A true value means that when this policy is executed, packages will be permanently deleted. false means that when the policy is executed packages will be deleted to the Trash Can. Defaults to `false`.

~>The Global Trash Can setting must be enabled if you want deleted items to be transferred to the Trash Can, see [Trash Can Settings](https://jfrog.com/help/r/jfrog-artifactory-documentation/trash-can-settings).

<a id="nestedatt--search_criteria"></a>
### Nested Schema for `search_criteria`

Required:

- `included_packages` (Set of String) Specify a pattern for a package name or an explicit package name on which you want the cleanup policy to run. Only one pattern or explicit name can be entered. To include all packages, use `**`. Example: `included_packages = ["**"]`
- `package_types` (Set of String) The package types that are cleaned up by the policy. Support: alpine, ansible, cargo, chef, cocoapods, composer, conan, conda, debian, docker, gems, generic, go, gradle, helm, helmoci, huggingfaceml, machinelearning, maven, npm, nuget, oci, puppet, pypi, sbt, swift, terraform, terraformbackend, yum.
- `repos` (Set of String) Specify one or more patterns for the repository name(s) on which you want the cleanup policy to run. You can also specify explicit repository names. Specifying at least one pattern or explicit name is mandatory. Only packages in repositories that match the pattern or explicit name will be deleted. For including all repos use `**`. Example: `repos = ["**"]`
- `included_projects` (Set of String) Enter the project keys for the projects on which you want the policy to run. To include repositories that are not assigned to any project, enter the project key `default`. Can be empty when `include_all_projects` is set to `true`.

~>This parameter is relevant only on the global level, for Platform Admins.

Optional:

- `created_before_in_days` (Number) The cleanup policy will delete packages based on how long ago they were created. For example, if this parameter is 5 then packages created more than 5 days ago will be deleted as part of the policy.
  > **Requires Artifactory 7.111.2 or later.**
~>JFrog recommends using the `created_before_in_days` condition to ensure that packages currently in use are not deleted.

- `last_downloaded_before_in_days` (Number) The cleanup policy will delete packages based on how long ago they were downloaded. For example, if this parameter is 5 then packages downloaded more than 5 days ago will be deleted as part of the policy.
  > **Requires Artifactory 7.111.2 or later.**
~>JFrog recommends using the `last_downloaded_before_in_days` condition to ensure that packages currently in use are not deleted.

~> Note: It also deletes packages that have never been downloaded, based on the creation date if it is older than the specified `last_downloaded_before_in_days` duration. 

- `created_before_in_months` (Number) The cleanup policy will delete packages based on how long ago they were created. For example, if this parameter is 2 then packages created more than 2 months ago will be deleted as part of the policy.

- `last_downloaded_before_in_months` (Number) The cleanup policy will delete packages based on how long ago they were downloaded. For example, if this parameter is 5 then packages downloaded more than 5 months ago will be deleted as part of the policy.

~>Note: It also deletes packages that have never been downloaded, based on the creation date if it is older than the specified `last_downloaded_before_in_months` duration.

~>**Deprecated:** Use `created_before_in_days` instead of `created_before_in_months`. Renamed to `created_before_in_days` starting in version 7.111.2.

~>**Deprecated:** Use `last_downloaded_before_in_days` instead of `last_downloaded_before_in_months`. Renamed to `last_downloaded_before_in_days` starting in version 7.111.2.

- `excluded_packages` (Set of String) Specify explicit package names that you want excluded from the policy. Only explicit names (and not patterns) are accepted.
- `excluded_properties` (Map of List of String) A key-value pair applied to the lead artifact of a package. Packages with this property will be excluded from deletion. Must have exactly one key with exactly one string value.
- `excluded_repos` (Set of String) Specify patterns for repository names or explicit repository names that you want excluded from the cleanup policy.
- `include_all_projects` (Boolean) Set this value to `true` if you want the policy to run on all Artifactory projects. The default value is `false`.

~>This parameter is relevant only on the global level, for Platform Admins.

- `included_properties` (Map of List of String) A key-value pair applied to the lead artifact of a package. Packages with this property will be deleted. Must have exactly one key with exactly one string value.
- `keep_last_n_versions` (Number) Set a value for the number of latest versions to keep. The cleanup policy will remove all versions prior to the number you select here. The latest version is always excluded.

~>Versions are determined by creation date.

~>Not all package types support this condition. If you include a package type in your policy that is not compatible with this condition, a validation error (400) is returned. For information on which package types support this condition, see [here](https://jfrog.com/help/r/jfrog-platform-administration-documentation/retention-policies/package-types-coverage).


## Import

Import is supported using the following syntax:

```shell
terraform import artifactory_package_cleanup_policy.my-cleanup-policy my-policy

terraform import artifactory_package_cleanup_policy.my-cleanup-policy my-policy:myproj
```
